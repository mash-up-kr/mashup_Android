plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    lintOptions {
        disable "JvmStaticProvidesInObjectDetector", "FieldSiteTargetOnQualifierAnnotation",
                "ModuleCompanionObjects", "ModuleCompanionObjectsNotInModuleParent"
    }
    signingConfigs {
        release {
            storeFile file('mashupKey')
            storePassword 'mashup1234!'
            keyPassword 'mashup1234!'
            keyAlias 'key'
        }
    }
    compileSdkVersion compileVersion

    defaultConfig {
        applicationId "com.mashup"
        minSdkVersion minVersion
        targetSdkVersion targetVersion
        versionCode 11
        versionName "1.0.6"

        testInstrumentationRunner "com.mashup.core.testing.MashUpTestRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    sourceSets {
        release {
            java.srcDirs = ['src/main/java', 'src/release/java']
        }

        debug {
            java.srcDirs = ['src/main/java', 'src/debug/java']
        }
    }

    buildFeatures {
        dataBinding true
        compose true
    }

    buildTypes {
        debug {
            minifyEnabled false
            buildConfigField "Boolean", "DEBUG_MODE", "true"
            signingConfig signingConfigs.debug
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            buildConfigField "Boolean", "DEBUG_MODE", "false"
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xuse-experimental=androidx.compose.animation.ExperimentalAnimationApi",
                "-Xuse-experimental=androidx.compose.foundation.ExperimentalFoundationApi"
        ]
    }
    composeOptions {
        kotlinCompilerExtensionVersion composeVersion
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation project(':core:ui')
    implementation project(":core:model")
    implementation project(":core:testing")
    implementation project(":core:common")

    // ml Kit
    implementation "com.google.mlkit:barcode-scanning:$barcodeSacnnerVersion"

    // camera
    implementation "androidx.camera:camera-camera2:$cameraVersion"
    implementation "androidx.camera:camera-lifecycle:$cameraVersion"
    implementation "androidx.camera:camera-view:$cameraVersion"

    // google firebase
    implementation platform("com.google.firebase:firebase-bom:$firebaseVersion")
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation "com.google.firebase:firebase-crashlytics-ktx"

    // hilt
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"

    // network
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpInterceptorVerison"

    // image
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    // DataStore
    implementation "androidx.datastore:datastore-preferences:$dataSourceVersion"

    // serialization
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"
    implementation "com.squareup.moshi:moshi-adapters:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
}